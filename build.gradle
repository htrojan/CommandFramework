import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext{
        kotlin_version = "1.1.1"
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'com.github.rodionmoiseev.gradle.plugins', name: 'idea-utils', version: '0.2'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.3"
    id "org.jetbrains.kotlin.jvm" version "1.1.1"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.junit.platform.gradle.plugin'


repositories {
    mavenCentral()
    jcenter()
    maven{
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven{
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceSets {
    main.kotlin.srcDirs = ['src']
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['tests']
    test.kotlin.srcDirs = ['tests']
}

group = project.property("group")
version = project.property("version")
sourceCompatibility = project.property("sourceversion")

dependencies {
    compile group: 'org.spigotmc', name: 'spigot-api', version: project.property("apiversion")
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-M3")
    testCompile("org.junit.platform:junit-platform-launcher:1.0.0-M3")
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "com.nhaarman:mockito-kotlin:1.4.0"

    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-M3")
}

processResources {
    filter ReplaceTokens, tokens: [
            "apiversion": project.property("apiversion"),
            "version"   : project.property("version")
    ]
}

shadowJar {
    dependencies {
        include(dependency('org.jetbrains.kotlin:.*:.*'))
    }
}


